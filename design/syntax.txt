<program>       -> ( <function> )+ EOF

<function>      -> "fn" IDENTIFIER "(" <parameters>? ")" ("->" <type>)? <block>
<parameters>    -> <parameter> ("," <parameter>)*
<parameter>     -> IDENTIFIER ":" <type>

<block>         -> "{" (<statement> ";")* <statement>? "}"

<statement>     -> <declaration>
                 | <expression>

<declaration>   -> "let" IDENTIFIER ":" <type> "=" <expression>

<expression>    -> <block_expr>
                 | <blockless_expr>

<block_expr>    -> <block>
                 | <if>
                 | <loop>

<if>            -> "if" <expression> <block> ("else" ( <block> | <if> ))?

<loop>          -> "for" IDENTIFIER ":" <type> "in" <expression> <block>
                 | "while" <expression> <block>

<blockless_expr> -> <attribution>
	             | "return" <expression>
	             | "debug" <expression>

<attribution>   -> <range> (<attrib_op> <attribution>)?                                 // associativo pela direita
<range>         -> <logic_or> (".." <logic_or>)?
<logic_or>      -> <logic_and> ( "||" <logic_and> )*
<logic_and>     -> <equality> ( "&&" <equality> )*
<equality>      -> <comparison> ( ( "!=" | "==" ) <comparison> )*
<comparison>    -> <term> ( ( ">" | ">=" | "<" | "<=" ) <term> )*
<term>          -> <factor> ( ( "-" | "+" ) <factor> )*
<factor>        -> <exponent> ( ( "" | "/" ) <exponent> )*
<exponent>      -> <unary> ("^" <exponent>)?                                            // associativo pela direita
<unary>         -> ("!" | "-" | "#") <unary> | <call>                                   // associativo pela direita
<call>          -> <primary> ( "(" arg_list? ")" )*
<primary>       -> "true" | "false" | NUMBER | STRING | IDENTIFIER
                 | "(" <expression> ")"

<arg_list>      -> <expression> ("," <expression>)*

<arg_list>      -> <expression> ("," <expression>)*
<type>          -> IDENTIFIER

<attrib_op>     -> "=" | "+=" | "-=" | "*=" | "/=" | "^="